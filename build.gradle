plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

def forge_loader_version = "${forge_version.takeWhile { it != '.' }}"

def minecraft_x_version = "${minecraft_version}"

if (minecraft_x_version.count('.') >= 2) {
    minecraft_x_version = minecraft_version.substring(0, minecraft_version.length() - 1) + "x"
} else {
    minecraft_x_version = minecraftVersion + '.x'
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    java.withSourcesJar()
    java.withJavadocJar()
        
    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        maven {
            name = 'Minecraft'
            url = 'https://libraries.minecraft.net/'
        }
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            name = 'TerraformersMC / Mod Menu'
            url = 'https://maven.terraformersmc.com/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
    
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(21)
    }

    processResources {
        def expandProps = [
                "group": project.group,
                "version": mod_version,
                "minecraft_version": minecraft_version,
                "minecraft_x_version": minecraft_x_version,
                "forge_version": forge_version,
                "forge_loader_version": forge_loader_version,
                "fabric_loader_version": fabric_loader_version,
                "neoforge_version": neoforge_version,
                "mod_version": mod_version,
                "mod_name": mod_name,
                "mod_author": mod_author,
                "mod_id": mod_id,
                "mod_homepage": mod_homepage,
                "mod_source": mod_source,
                "mod_issues": mod_issues,
                "mod_desc": mod_desc,
                "mod_license": mod_license
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    tasks.withType(GenerateModuleMetadata).configureEach {

        enabled = false
    }
}
